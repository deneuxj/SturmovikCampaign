type Convoy {
	entity LeadCarEntity
	trigger LeadCarDamaged
	trigger ActivateGroup
	trigger DeactivateGroup
	trigger DeleteLeadCar
	trigger TriggerGates
}
TruckInConvoy truck_in_convoy(Convoy, int)
WhileEnemyClose while_enemy_close_of_convoy(Convoy)

type TruckInConvoy {
	entity Entity
	trigger Damaged
	trigger Delete
}

type ActiveWaypoint {
	trigger Waypoint
	trigger Gate
	trigger Activate
	trigger Deactivate
}
Convoy convoy_at_waypoint(ActiveWaypoint)
WhileEnemyClose enemy_close_at_waypoint(ActiveWaypoint)
predicate path(ActiveWaypoint, ActiveWaypoint)
predicate path_start(ActiveWaypoint)
predicate path_end(ActiveWaypoint)
Timer timer_between_waypoints(ActiveWaypoint, ActiveWaypoint)

type WhileEnemyClose {
	trigger StartMonitoring
	trigger StopMonitoring
	trigger Deactivate
	trigger Activate
	trigger WakeUp
	trigger Sleep
	trigger Proximity
}
Convoy convoy_of_enemy_close(WhileEnemyClose)

type Timer {
	trigger Start
	trigger Stop
	trigger Elapsed
}

type AtDestination {
	trigger LeaderArrived
	trigger Destroyed
}
predicate at_destination(AtDestination, TruckInConvoy)

type VirtualConvoy {
	trigger Start // 1x counter
	trigger Destroyed // This is a non-wraping counter set to (num of trucks in convoy) + 1
	trigger Arrived // 1x counter
}
predicate result(VirtualConvoy)


// Convoy <-> TruckInConvoy
columns += for (convoy, position, truck) in truck_in_convoy -> (truck.Entity, convoy.LeadCarEntity, position)

// Convoy <-> ActiveWaypoint
object += for (wp, convoy) in convoy_at_waypoint for wp2 in path*(wp) -> (convoy.LeadCarEntity, wp2.Waypoint)
target += for (wp, convoy) in convoy_at_waypoint for wp2 in path*(wp) -> (convoy.TriggerGates, wp2.Gate)

// Convoy <-> WhileEnemyClose
object += for (convoy, wec) in convoy_of_enemy_close -> (wec.Proximity, convoy.LeadCarEntity)
target += for (convoy, wec) in convoy_of_enemy_close -> (wec.WakeUp, convoy.ActivateGroup)
target += for (convoy, wec) in convoy_of_enemy_close -> (wec.Sleep, convoy.DeactivateGroup)

// Convoy <-> Timer
//nothing

// Convoy <-> Convoy
target += for (current, next) in path -> (convoy_at_waypoint(current).LeadCarDamaged, convoy_at_waypoint(next).DeleteLeadCar)
target += for (current, next) in path -> (convoy_at_waypoint(current).DeleteLeadCar, convoy_at_waypoint(next).DeleteLeadCar)

// TruckInConvoy <-> TruckInConvoy
target += for (curr, next) in path
          for (position, truck) in truck_in_convoy(convoy_at_waypoint(curr), _, _)
          for truck2 in truck_in_convoy(convoy_at_waypoint(next), position, _) -> (truck.Damaged, truck2.Delete)
target += for (curr, next) in path
          for (position, truck) in truck_in_convoy(convoy_at_waypoint(curr), _, _)
          for truck2 in truck_in_convoy(convoy_at_waypoint(next), position, _) -> (truck.Delete, truck2.Delete)

// ActiveWaypoint <-> ActiveWaypoint
target += for (curr, next) in path -> (curr.Waypoint, next.Waypoint)

// ActiveWaypoint
target += for (wp) in path_start
          for out in result -> (out.Start, wp.Activate)

// ActiveWaypoint <-> WhileEnemyClose
target += for (wp, convoy) in convoy_at_waypoint -> (wp.Activate, while_enemy_close_of_convoy(convoy).StartMonitoring)
target += for (wp, convoy) in convoy_at_waypoint -> (wp.Deactivate, while_enemy_close_of_convoy(convoy).StopMonitoring)

// ActiveWaypoint <-> Timer
target += for (curr, next) in path -> (timer_between_waypoints(curr, next).Elapsed, next.Activate)
target += for (curr, next) in path -> (timer_between_waypoints(curr, next).Elapsed, curr.Deactivate)
target += for (curr, next) in path -> (curr.Activate, timer_between_waypoints(curr, next).Start)

// WhileEnemyClose <-> WhileEnemyClose
target += for (curr, next) in path for wp in path*(next) -> (while_enemy_close_of_convoy(convoy_at_waypoint(curr)).WakeUp, while_enemy_close_of_convoy(convoy_at_waypoint(wp)).Deactivate)
target += for (curr, next) in path for wp in path*(next) -> (while_enemy_close_of_convoy(convoy_at_waypoint(curr)).Sleep, while_enemy_close_of_convoy(convoy_at_waypoint(wp)).Activate)

// ... <-> VirtualConvoy
target += for (_, convoy) in convoy_at_waypoint
          for out in result -> (convoy.LeadCarDamaged, out.Destroyed)
target += for (convoy, _, truck) in truck_in_convoy
          for out in result -> (truck.Damaged, out.Destroyed)
target += for wp in path_end
          for out in result -> (wp.Waypoint, out.Arrived)
target += for wp in path_end
          for out in result -> (wp.Activate, out.Arrived)

// AtDestination
target += for (atd, truck, result) in at_destination -> (result.Arrived, atd.LeaderArrived)
                                                     -> (truck.Damaged, atd.Destroyed)